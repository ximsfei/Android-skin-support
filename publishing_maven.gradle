apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

version = publishVersion
group = "skin.support"

def siteUrl = 'https://github.com/ximsfei/Android-skin-support'
def gitUrl = 'https://github.com/ximsfei/Android-skin-support.git'

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives'] //When uploading configuration files
    pkg {
        repo = "skin-support"
        name = moduleName
        desc = descContent
        websiteUrl = siteUrl
        issueTrackerUrl = 'https://github.com/ximsfei/Android-skin-support/issues'
        vcsUrl = gitUrl
        licenses = ['MIT']
        labels = ['skin', 'support', 'theme', 'android']
        publicDownloadNumbers = true
    }
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                name moduleName
                url siteUrl
                licenses {
                    license {
                        name 'MIT'
                        url 'https://github.com/ximsfei/Android-skin-support/blob/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id 'ximsfei'
                        name 'Pengfeng Wang'
                        email 'ximsfei@gmail.com'
                    }
                }
                scm {
                    connection 'https://github.com/ximsfei/Android-skin-support.git'
                    developerConnection 'https://github.com/ximsfei/Android-skin-support.git'
                    url siteUrl

                }
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

task findConventions {
    doLast {
        println project.getConvention()
    }
}